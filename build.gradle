buildscript {
    ext {
        gradleNodeVersion = '1.2.0'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:${gradleNodeVersion}"
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
    }
}

ext {
    glowingBearVersion = '0.0.1-SNAPSHOT'
    nodeVersion = '8.10.0'
    yarnVersion = '1.5.1'
    gradleWrapperVersion = '4.0'
}

version = glowingBearVersion
group = 'nl.thehyve'

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'distribution'
apply plugin: 'com.moowork.node'
apply plugin: 'net.linguica.maven-settings'
apply plugin: 'maven-publish'

repositories {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

node {
    version = nodeVersion
    yarnVersion = yarnVersion
    download = true
}

task yarnPackage(type: YarnTask, dependsOn: 'yarn') {
  group = 'application'
  description = 'Builds the application'
  args = ['run', 'package']
}
distTar.dependsOn(yarnPackage)

task yarnTest(type: YarnTask, dependsOn: 'yarn') {
  group = 'verification'
  description = 'Executes user interface unit tests'
  args = ['run', 'testOnce']
}
check.dependsOn(yarnTest)

clean {
    delete fileTree(dir: 'dist')
}

distributions {
    main {
        contents {
            from 'dist'
            include '**'
        }
    }
}

distZip.enabled = false

publishing {
    repositories {
        maven {
            name 'nl.thehyve.nexus'
            url 'https://repo.thehyve.nl/content/repositories/snapshots/'
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId 'glowing-bear'
            artifact distTar // Publish the output of the distTar task
        }
    }
}
publishToMavenLocal.dependsOn 'distTar'
publish.dependsOn 'distTar'
distTar.baseName = 'glowing-bear'

