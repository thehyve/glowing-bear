import groovy.json.JsonSlurper

buildscript {
    ext {
        gradleNodeVersion = '1.2.0'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:${gradleNodeVersion}"
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
    }
}

def versionFile = new File('src/app/config/config.default.json')
def versionObject = new JsonSlurper().parseText(versionFile.text)

ext {
    glowingBearVersion = versionObject['app-version']
    nodeVersion = '10.12.0'
    yarnVersion = '1.10.1'
    gradleWrapperVersion = '4.0'
}

version = glowingBearVersion
group = 'nl.thehyve'

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'distribution'
apply plugin: 'com.moowork.node'
apply plugin: 'net.linguica.maven-settings'
apply plugin: 'maven-publish'

repositories {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

node {
    version = nodeVersion
    yarnVersion = yarnVersion
    download = true
}

task yarnPackage(type: YarnTask, dependsOn: 'yarn') {
  group = 'application'
  description = 'Builds the application'
  args = ['run', 'package']
}
distTar.dependsOn(yarnPackage)

task yarnLint(type: YarnTask, dependsOn: 'yarn') {
  group = 'verification'
  description = 'Performs static code analysis'
  args = ['run', 'lint']
}
check.dependsOn(yarnLint)

task yarnTest(type: YarnTask, dependsOn: 'yarn') {
  group = 'verification'
  description = 'Executes user interface unit tests'
  args = ['run', 'testOnce']
}
check.dependsOn(yarnTest)

task yarnCodecov(type: YarnTask, dependsOn: 'yarn') {
  group = 'verification'
  description = 'Report code coverage'
  args = ['run', 'report-coverage']
}

clean {
    delete fileTree(dir: 'dist')
    delete 'coverage'
}

distributions {
    main {
        baseName = 'glowing-bear'
        contents {
            from 'dist'
            include '**'
        }
    }
    cypress {
        baseName = 'glowing-bear-cypress-tests'
        contents {
            from 'cypress'
            include '**'
        }
    }
}

distZip.enabled = false
cypressDistTar.enabled = false

publishing {
    repositories {
        maven {
            name 'nl.thehyve.nexus'
            url 'https://repo.thehyve.nl/content/repositories/releases/'
        }
    }
    publications {
        app(MavenPublication) {
            artifactId 'glowing-bear'
            artifact distTar // Publish the output of the distTar task
        }
        cypressTests(MavenPublication) {
            artifactId 'glowing-bear-cypress-tests'
            artifact cypressDistZip
        }
    }
}
publishToMavenLocal.dependsOn 'distTar'
publish.dependsOn 'distTar'
